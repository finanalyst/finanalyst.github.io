use v6.d;
use Test;
use RakuDoc::Render;

plan 6;

my $ast;
my $rv;
my RakuDoc::Processor $rdp .= new(:test);

$ast = Q:to/QAST/.AST;
    =numhead1 Test text
    QAST
$rv = $rdp.render( $ast, :pre-finalised );
like $rv.body.Str, /
    '<numhead>' .+?
    'caption: ｢Test text｣' .+
    'contents: ｢Test text｣' .+
    'id: ｢' .+
    'level: ｢1｣' .+
    'numeration:' .+ '1.' .+
    'target: ｢Test_text｣' .+
    'toc: ｢True｣' .+
    '</numhead>'
    /, 'numhead template works';

$ast = Q:to/QAST/.AST;
    =begin rakudoc
    =numhead3 This is a level 3 numheader

    Some text in a paragraph
    =end rakudoc
    QAST
$rv = $rdp.render( $ast, :pre-finalised );
like $rv.body.Str, /
     '<numhead>' .+?
    'caption: ｢This is a level 3 numheader｣' .+
    'contents: ｢This is a level 3 numheader｣' .+
    'level: ｢3｣' .+
    'numeration:' .+ '0.0.1' .+
    '</numhead>'
    /, 'increment of 3 level only works';

$ast = Q:to/AST/.AST;
    =begin rakudoc
    =numitem1 Visito
    =numitem2 Veni
    =numitem2 Vidi
    =numitem2 Vici
    =end rakudoc
    AST

$rv = $rdp.render( $ast, :pre-finalised );
like $rv.body.Str, /
    '<numitem-list>' .+
    '<numitem>' .+? 'contents' .+? 'Visito' .+ 'numeration' .+ '1.｣' .+
    '<numitem>' .+? 'contents' .+? 'Veni' .+ 'numeration' .+ '1.1.｣' .+
    '<numitem>' .+? 'contents' .+? 'Vidi' .+ 'numeration' .+ '1.2.｣' .+
    '<numitem>' .+? 'contents' .+? 'Vici' .+ 'numeration' .+ '1.3.｣' .+
    '</numitem-list>'
    /, 'simple ordered list';

$ast = Q:to/AST/.AST;
    =begin rakudoc
    The options are:
    =numitem1 Liberty
    =numitem1 Death
    =numitem1 Beer

    The tools are:

    =numitem1 Revolution
    =numitem1 Deep-fried peanut butter sandwich
    =numitem1 Keg
    =end rakudoc
    AST
$rv = $rdp.render( $ast, :pre-finalised );
like $rv.body.Str, /
    'para' .+? 'contents' .+ 'The options are:' .+
    '<numitem>' .+? 'contents' .+? 'Liberty' .+ 'numeration' .+ '1.｣' .+
    '<numitem>' .+? 'contents' .+? 'Death' .+ 'numeration' .+ '2.｣' .+
    '<numitem>' .+? 'contents' .+? 'Beer' .+ 'numeration' .+ '3.｣' .+
    'para' .+? 'contents' .+ 'The tools are:' .+
    '<numitem>' .+? 'contents' .+? 'Revolution' .+ 'numeration' .+ '1.｣' .+
    '<numitem>' .+? 'contents' .+? 'Deep-fried peanut butter sandwich' .+ 'numeration' .+ '2.｣' .+
    '<numitem>' .+? 'contents' .+? 'Keg' .+ 'numeration' .+ '3.｣'
    /, 'two ordered lists';

$ast = Q:to/AST/.AST;
    =begin rakudoc
    =numitem1 Retreat to remote Himalayan monastery
    =numitem1 Learn the hidden mysteries of space and time

    I<????>
    =for numitem1 :continued
    Prophet!
    =end rakudoc
    AST
$rv = $rdp.render( $ast, :pre-finalised );
like $rv.body.Str, /
    '<numitem>' .+? 'contents' .+? 'Retreat to remote Himalayan monastery' .+ 'numeration' .+ '1.｣' .+
    '<numitem>' .+? 'contents' .+? 'Learn the hidden mysteries of space and time' .+ 'numeration' .+ '2.｣' .+
    'para' .+? 'contents' .+ 'markup-I' .+ 'contents' .+ '????' .+ '/markup-I' .+
    '<numitem>' .+? 'contents' .+? 'Prophet!' .+ 'numeration' .+ '3.｣'
    /, 'continued ordered list';

$ast = Q:to/QAST/.AST;
    =begin rakudoc
    =numdefn Happy
    When you're I<not> blue.

    =numdefn Blue
    When you're not happy.
    =end rakudoc
    QAST
$rv = $rdp.render( $ast, :pre-finalised );
like $rv.body.Str, /
    '<numdefn-list>' .+
    '<numdefn>' .+
    'contents:' .+ "When you're <markup-I>" .+ 'not' .+ '/markup-I' .+ ' blue' .+
    'numeration:' .+ '｢1.｣' .+
    'target:' .+ 'defn_Happy' .+
    'term:' .+ 'Happy' .+
    '</numdefn>' .+
    '<numdefn>' .+
    'contents:' .+ "When you're not happy" .+
    'numeration:' .+ '｢2.｣' .+
    'target:' .+ 'defn_Blue' .+
    'term:' .+ 'Blue' .+
    '</numdefn>' .+
    '</numdefn-list>'
    /, 'Basic definitions';

done-testing;
